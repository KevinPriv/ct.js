
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "io.franzbecker:gradle-lombok:1.10"
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "io.franzbecker.gradle-lombok"

version = "0.11.6-SNAPSHOT"
group = "com.chattriggers.ctjs" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ctjs"

apply plugin: "java"
compileJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

configurations {
    provided
    // Make compile extend from our provided configuration so that things added to bundled end up on the compile classpath
    compile.extendsFrom(provided)
}


minecraft {
    version = "1.8.9-11.15.1.1722"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_22"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

dependencies {
    compile files("libs/nashorn.jar")
    provided files("libs/sentry.jar")
    provided group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.9.3"
    //compile 'org.spongepowered:mixin:0.7.5-SNAPSHOT-20171018.121934-1'
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
      
    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // the 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
    //provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // the deobf configurations:  'deobfCompile' and 'deobfProvided' are the same as the normal compile and provided,
    // except that these dependencies get remapped to your current MCP mappings
    //deobfCompile 'com.mod-buildcraft:buildcraft:6.0.8:dev'
    //deobfProvided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

sourceSets {
    main {
        refMap = "mixins.myplugin.refmap.json"
    }
}

jar {
    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.ctjs.json'
    )
}


processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version":project.version, "mcversion":project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}


/*
 * Userdocs
 */

import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask, dependsOn: compileJava) {
    description "Delomboks source code in order to compile javadocs"
    group "Documentation"

    ext.outputDir = file("$buildDir/src-delomboked")
    sourceSets.main.java.srcDirs.each {
        args(it, "-f", "pretty", "-d", outputDir)
    }
}

task userdocs(type: Javadoc) {
    description = "Generate the docs to be used by scripters, excludes internal classes"
    group = "documentation"

    setDependsOn(["delombok"])
    options.links("https://docs.oracle.com/javase/8/docs/api/")
    source = "$buildDir/src-delomboked"
    classpath += configurations.compile
    exclude "com/chattriggers/ctjs/commands/**.java"
    exclude "com/chattriggers/ctjs/listeners/**.java"
    exclude "com/chattriggers/ctjs/utils/*/**.java"
    exclude "com/chattriggers/ctjs/*.java"
    exclude "com/chattriggers/ctjs/triggers/TriggerType.java"
    exclude "com/chattriggers/ctjs/modules/**.java"
    exclude "com/chattriggers/ctjs/loader/**.java"
    exclude "com/chattriggers/ctjs/handlers/GuiHandler.java"
    exclude "com/chattriggers/ctjs/utils/*.java"
}